import com.vanniktech.maven.publish.SonatypeHost
import org.jetbrains.kotlin.gradle.ExperimentalWasmDsl

plugins {
    id("org.jetbrains.kotlin.multiplatform") version "2.2.0"
    id("com.android.kotlin.multiplatform.library") version "8.10.1"
    id("com.android.lint") version "8.10.1"
    id("org.jetbrains.kotlin.plugin.serialization") version "2.2.0"
    id("maven-publish")
    id("com.vanniktech.maven.publish") version "0.30.0"
    id("org.jetbrains.dokka") version "1.9.20"
}

kotlin {
    androidLibrary {
        namespace = "com.daiatech.serialization.ktx"
        compileSdk = 36
        minSdk = 24

        withHostTestBuilder {
        }

        withDeviceTestBuilder {
            sourceSetTreeName = "test"
        }.configure {
            instrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        }
    }

    jvm()

    @OptIn(ExperimentalWasmDsl::class)
    wasmJs {
        browser()
        binaries.executable()
    }

    listOf(
        iosX64(),
        iosArm64(),
        iosSimulatorArm64()
    ).forEach { iosTarget ->
        iosTarget.binaries.framework {
            baseName = "specificationKit"
        }
    }

    sourceSets {
        commonMain {
            kotlin.srcDir("src/main/kotlin")
            dependencies {
                implementation("org.jetbrains.kotlinx:kotlinx-serialization-json:1.8.0")
            }
        }

        commonTest {
            kotlin.srcDir("src/test/kotlin")
            dependencies{
                implementation("org.jetbrains.kotlin:kotlin-test:2.2.0")
            }
        }
    }
}

apply {
    from(rootProject.file("install-git-hooks.gradle"))
}


group = "io.github.karya-inc"
version = "0.0.4"
mavenPublishing {
    val artifactId = "serialization-ktx"
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    signAllPublications()

    coordinates(
        groupId = group.toString(),
        artifactId = artifactId,
        version = version.toString()
    )

    pom {
        name.set(artifactId)
        description.set("A Kotlin serialization utility library")
        url.set("hhttps://github.com/karya-inc/serialization-ktx.git")

        licenses {
            license {
                name.set("GNU license")
                url.set("https://opensource.org/license/gpl-3-0")
            }
        }

        developers {
            developer {
                id.set("divyansh@karya.in")
                name.set("Divyansh Kushwaha")
                email.set("divyansh@karya.in")
            }
        }

        scm {
            connection.set("scm:git:ssh://git@github.com/karya-inc/serialization-ktx.git")
            developerConnection.set("scm:git:ssh://git@github.com/karya-inc/serialization-ktx.git")
            url.set("https://github.com/karya-inc/serialization-ktx.git")
        }
    }
}
